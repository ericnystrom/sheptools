#!/usr/bin/awk -f
#
# shep-opinion-age: calculate and add an approximate "opinion age" to the output of a
# Shepards breakdown generated by shep2csv that has had SCDB "caseid" information added.
#
# by Eric Nystrom, http://ericnystrom.org
#   started: January 24, 2018
#   license: MIT License
#
# USAGE:
#
#   shep-opinion-age scalia-shepdata-with-scdb.tsv > OUTPUT.tsv
#
# Where the input file has a header on the first line, the "year_num"
# field in $8, and caseid (consisting of a year, a hyphen, and another
# number) in $10.


BEGIN {
    FS = "\t"
    OFS = "\t"
    
}

{
    ## First line: header
    if (FNR == 1) {
	print $0, "opinion_age"
	next
    }

    # Only do this if we actually have a proper year in the year_num
    # field in $8; otherwise set blank
    if ($8 ~ /^[0-9]{4}$/ ) {
	
	# Split apart the SCDB "caseid" field to get the year. Note
	# this year is the court term, so sometimes decisions don't
	# actually get handed down until the next calendar year, but
	# this is fine for a rough measure.
	split($10, a, "-")

	# Then opinion age is just the citation's year minus the court term year
	opinage = ($8 - a[1])
	
	# Catch negative numbers created by bad data, try turning this
	# off for debugging purposes
	if (opinage < 0) {
	    opinage = ""
	}
    } else {
	opinage = ""
    }
    print $0, opinage

}

	    
